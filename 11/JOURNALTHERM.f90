	PROGRAM JOURNALTHERM
	DIMENSION X(200),Y(200),P(20000),H(20000),T(20000),EDA(20000),POLD(20000)
	DATA EDA0,RO,C,AJ,B,R,RATIO,AN,EPSON/0.05,890.0,1870.0,4.184,1.0E-1,6.0E-2,0.003,3.0E3,0.7/
	OPEN(7,FILE='FILM.DAT',STATUS='UNKNOWN')
	OPEN(8,FILE='PRESSURE.DAT',STATUS='UNKNOWN')
	OPEN(9,FILE='TEM.DAT',STATUS='UNKNOWN')
	OPEN(10,FILE='EDA.DAT',STATUS='UNKNOWN')
	PI=3.1415926
	N=61
	M=41
	DX=2.0*PI/FLOAT(N-1)
	DY=1./FLOAT(M-1)
	ALFA1=R/B
	C0=RATIO*R
	U=R*AN*2.0*PI/60.0
	ALFA=(R/B*DX/DY)**2
	A=U*R*EDA0/(2.0*AJ*RO*C*C0**2)
	T0=303.0
	KG=0
	CALL INIT(N,M,DX,DY,A,T0,EPSON,X,Y,H,T,EDA)
10	CALL SUBP(N,M,DX,EPSON,ALFA,H,P,EDA)
	CALL THERM(N,M,A,ALFA1,DX,DY,EDA0,T0,X,Y,P,H,T,EDA)
	IF(KG.EQ.0)THEN
	CALL PSAVE(KG,N,M,P,POLD,ERO)
	KG=1
	GOTO 10
	ENDIF
	CALL PSAVE(KG,N,M,P,POLD,ERO)
	WRITE(*,*)'EROP,KG=',ERO,KG
	KG=KG+1
	IF(ERO.GT.1.E-6.AND.KG.LT.10)GOTO 10
	CALL OUTPUT(N,M,A,T0,X,Y,H,P,T,EDA)
	STOP
	END

	SUBROUTINE INIT(N,M,DX,DY,A,T0,EPSON,X,Y,H,T,EDA)
	DIMENSION X(N),Y(M),H(N,M),T(N,M),EDA(N,M)
	DO I=1,N
	X(I)=(I-1.0)*DX
	ENDDO
	DO J=1,M
	Y(J)=-0.5+(J-1)*DY
	DO I=1,N
	H(I,J)=1.0+EPSON*COS(X(I))
	T(I,J)=T0/A
	EDA(I,J)=1.0
	ENDDO
	ENDDO
	RETURN
	END

	SUBROUTINE SUBP(N,M,DX,EPSON,ALFA,H,P,EDA)
	DIMENSION H(N,M),P(N,M),EDA(N,M)
	DO I=2,N-1
	DO J=2,M-1
	P(I,J)=0.5
	ENDDO   
	ENDDO
	DO J=1,M
	P(1,J)=0.0
	P(N,J)=0.0
	ENDDO
	DO I=1,N
	P(I,1)=0.0
	P(I,M)=0.0
	ENDDO
	IK=0
10	C1=0.0
	ALOAD=0.0
	DO I=2,N-1
	I1=I-1
	I2=I+1
	DO J=2,M-1
	PD=P(I,J)
	J1=J-1
	J2=J+1
	A1=(0.5*(H(I2,J)+H(I,J)))**3/(0.5*(EDA(I2,J)+EDA(I,J)))
	A2=(0.5*(H(I,J)+H(I1,J)))**3/(0.5*(EDA(I,J)+EDA(I1,J)))
	A3=ALFA*H(I,J)**3/(0.5*(EDA(I,J2)+EDA(I,J)))
	A4=ALFA*H(I,J)**3/(0.5*(EDA(I,J)+EDA(I,J1)))
	A5=A1*P(I2,J)+A2*P(I1,J)+A3*P(I,J2)+A4*P(I,J1)
	A6=A1+A2+A3+A4
	P(I,J)=(-DX*(H(I2,J)-H(I1,J))+A5)/A6
	P(I,J)=0.7*PD+0.3*P(I,J)
	IF(P(I,J).LT.0.0)P(I,J)=0.0
	C1=C1+ABS(P(I,J)-PD)
	ALOAD=ALOAD+P(I,J)
	ENDDO
	ENDDO
	IK=IK+1
	C1=C1/ALOAD
	IF(C1.GT.1.E-6)GOTO 10
	RETURN
	END

	SUBROUTINE PSAVE(KG,N,M,P,POLD,ERO)
	DIMENSION P(N,M),POLD(N,M)
	IF(KG.EQ.0)GOTO 10
	ERO=0.0
	EROMAX=-1.0
	W=0.0
	DO I=1,N
	DO J=1,M
	AE=ABS(P(I,J)-POLD(I,J))
	ERO=ERO+AE
	IF(AE.GT.EROMAX)THEN
	II=I
	JJ=J
	EROMAX=AE
	ENDIF
	W=W+ABS(P(I,J))
	ENDDO
	ENDDO
	ERO=ERO/W
10	DO I=1,N
	DO J=1,M
	POLD(I,J)=P(I,J)
	ENDDO
	ENDDO
	RETURN
	END

	SUBROUTINE THERM(N,M,A,ALFA1,DX,DY,EDA0,T0,X,Y,P,H,T,EDA)
	DIMENSION X(N),Y(M),H(N,M),P(N,M),T(N,M),EDA(N,M)
	KG=0
10	ERT=0.0
	DO I=2,N
	DO J=1,M/2+1
	TOLD=T(I,J)
	EDA(I,J)=EXP((ALOG(EDA0)+9.67)*(((A*T(I,J)-138.0)/(T0-138.0))**(-1.1)-1.0))
	IF(I.NE.N)DPDX=0.5*(P(I+1,J)-P(I-1,J))/DX
	IF(I.EQ.N)DPDX=0.5*(P(1,J)-P(I-1,J))/DX
	QX=0.5*H(I,J)-0.5*H(I,J)**3*DPDX
	DPDY=ALFA1*(P(I,J+1)-P(I,J))/DY
	IF(J.EQ.M/2+1)DPDY=0.0
	QY=-0.5*H(I,J)**3*DPDY
	AA=QX/DX*T(I-1,J)-ALFA1*QY*T(I,J+1)/DY
	AB=2.0*EDA(I,J)/H(I,J)
	AC=6.0*H(I,J)/EDA(I,J)*(DPDX**2+DPDY**2)
	BB=QX/DX-ALFA1*QY/DY
	T(I,J)=(AA+AB+AC)/BB
	IF(A*T(I,J).GE.403.)THEN
	WRITE(*,*)'T OVER THE LIMIT 100'
	WRITE(*,*)I,J,T(I,J)
	STOP 00001
	ENDIF
	T(I,J)=0.7*TOLD+0.3*T(I,J)
	ERT=ERT+ABS(T(I,J)-TOLD)/303.
	ENDDO
	ENDDO
	ERT=A*ERT/((N-1)*(M-1))
	KG=KG+1
	DO J=1,M
	T(1,J)=0.5*(T(1,J)+T(N,J))
	ENDDO
	WRITE(*,*)'ERT,KG=',ERT,KG
	IF(ERT.GT.1.E-3)GOTO 10
	DO I=1,N
	DO J=1,M/2
	T(I,M-J+1)=T(I,J)
	EDA(I,M-J+1)=EDA(I,J)
	ENDDO
	ENDDO
	RETURN
	END
	SUBROUTINE OUTPUT(N,M,A,T0,X,Y,H,P,T,EDA)
	DIMENSION X(N),Y(M),H(N,M),P(N,M),T(N,M),EDA(N,M)
	TMAX=0.0
	DO I=1,N
	DO J=1,M
	T(I,J)=A*T(I,J)-T0
	IF(T(I,J).GT.TMAX)TMAX=T(I,J)
	END DO
	ENDDO
	WRITE(*,*)'TAMX=',TMAX
	WRITE(7,30)X(1),(Y(J),J=1,M)
	WRITE(8,30)X(1),(Y(J),J=1,M)
	WRITE(9,30)X(1),(Y(J),J=1,M)
	WRITE(10,30)X(1),(Y(J),J=1,M)
	DO I=1,N
	WRITE(7,30)X(I),(H(I,J),J=1,M)
	WRITE(8,30)X(I),(P(I,J),J=1,M)
	WRITE(9,30)X(I),(T(I,J),J=1,M)
	WRITE(10,30)X(I),(EDA(I,J),J=1,M)
	ENDDO
30  FORMAT(42(1X,E12.6))
	RETURN
	END
